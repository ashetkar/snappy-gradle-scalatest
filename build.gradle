plugins {
    id "com.jfrog.bintray" version "1.0"
}

apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'groovy'

group = 'com.github.maiflai'

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from "$buildDir/docs/groovydoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.getProperty('bintrayUsername') : ''
    key = project.hasProperty('bintrayApiKey') ? project.getProperty('bintrayApiKey') : ''
    configurations = ['archives']
    dryRun = false
    publish = false
    pkg {
        repo = 'maven'
        name = 'gradle-scalatest'
        desc = 'A plugin to enable the use of scalatest in a gradle scala project.'
        websiteUrl = 'https://github.com/maiflai/gradle-scalatest'
        issueTrackerUrl = 'https://github.com/maiflai/gradle-scalatest/issues'
        vcsUrl = 'https://github.com/maiflai/gradle-scalatest.git'
        licenses = ['unlicense']
        labels = ['gradle', 'testing', 'scala']
        publicDownloadNumbers = true
        version {
            attributes = ['gradle-plugin': 'com.github.maiflai.scalatest:com.github.maiflai:gradle-scalatest']
            mavenCentralSync {
                user = project.hasProperty('sonatypeUsername') ? project.getProperty('sonatypeUsername') : ''
                password = project.hasProperty('sonatypePassword') ? project.getProperty('sonatypePassword') : ''
            }
        }

    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            pom.project {
                name 'gradle-scalatest'
                description 'gradle-scalatest is a Gradle plugin for executing scalatest tests'
                url 'https://github.com/maiflai/gradle-scalatest'

                scm {
                    url 'scm:git:https://github.com/maiflai/gradle-scalatest.git'
                }

                licenses {
                    license {
                        name 'unlicense'
                        url 'http://unlicense.org'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'maiflai'
                    }
                }
            }
        }
    }
}
